// *************************************
// P1X ZIG ENGINE
// by Krzysztof Krystian Jankowski
// github.com/w84death/p1x-zig-engine
// *************************************

const Mouse = @import("mouse.zig").Mouse;
const Render = @import("render.zig").Render;
const CONF = @import("config.zig").CONF;
const THEME = @import("../themes/mil.zig").Theme;
const Font = @import("font.zig").Font8x16;
const Vec2 = @Vector(2, i32);
const Rect = struct {
    w: i32,
    h: i32,
    x: i32,
    y: i32,
    pub fn init(w: i32, h: i32, x: i32, y: i32) Rect {
        return .{ .w = w, .h = h, .x = x, .y = y };
    }
};

inline fn vec2(x: i32, y: i32) Vec2 {
    return .{ x, y };
}
const PIVOT_PADDING = 24;
pub const Pivot = enum {
    center,
    top_left,
    top_right,
    bottom_left,
    bottom_right,
};
pub const Fui = struct {
    app_name: [:0]const u8 = CONF.THE_NAME,
    renderer: Render,
    pub fn init(buf: *[CONF.SCREEN_W * CONF.SCREEN_H]u32) Fui {
        return Fui{
            .renderer = Render.init(buf),
        };
    }
    pub inline fn pivot(self: *const Fui, p: Pivot) Vec2 {
        _ = self;
        return switch (p) {
            .center => vec2(CONF.SCREEN_W / 2, CONF.SCREEN_H / 2),
            .top_left => vec2(PIVOT_PADDING, PIVOT_PADDING),
            .top_right => vec2(CONF.SCREEN_W - PIVOT_PADDING, PIVOT_PADDING),
            .bottom_left => vec2(PIVOT_PADDING, CONF.SCREEN_H - PIVOT_PADDING),
            .bottom_right => vec2(CONF.SCREEN_W - PIVOT_PADDING, CONF.SCREEN_H - PIVOT_PADDING),
        };
    }
    pub inline fn pivotX(self: *const Fui, p: Pivot) i32 {
        return self.pivot(p)[0];
    }
    pub inline fn pivotY(self: *const Fui, p: Pivot) i32 {
        return self.pivot(p)[1];
    }

    pub fn draw_text(self: *Fui, s: []const u8, x: i32, y: i32, scale: i32, color: u32) void {
        var px = x;
        for (s) |chr| {
            if (chr >= 32 and chr < 95 + 32) {
                const bmh = Font[chr - 32];
                var dy: i32 = 0;
                while (dy < CONF.FONT_HEIGHT) : (dy += 1) {
                    var dx: i32 = 0;
                    while (dx < CONF.FONT_WIDTH) : (dx += 1) {
                        const bit: u6 = @intCast(dy * CONF.FONT_WIDTH + dx);
                        if ((bmh >> bit) & 1 != 0) {
                            const rx: i32 = @intCast(dx * scale);
                            const ry: i32 = @intCast(dy * scale);
                            self.renderer.draw_rect(px + rx + 2, y + ry + 2, scale, scale, THEME.SHADOW);
                            self.renderer.draw_rect(px + rx, y + ry, scale, scale, color);
                        }
                    }
                }
            }
            px += @as(i32, CONF.FONT_WIDTH) * scale + 1;
        }
    }
    pub fn text_length(self: *Fui, s: []const u8, scale: i32) i32 {
        _ = self;
        const len: i32 = @intCast(s.len);
        return len * scale * CONF.FONT_WIDTH + (len - 1) * 2;
    }
    pub fn text_center(self: *Fui, s: []const u8, scale: i32) Vec2 {
        _ = self;
        const len: i32 = @intCast(s.len);
        return vec2(@divFloor(len * scale * CONF.FONT_WIDTH + (len - 2) * scale, 2), @divFloor(scale * CONF.FONT_HEIGHT, 2));
    }
    pub fn draw_cursor_lines(self: *Fui, mouse: Vec2) void {
        self.renderer.draw_line(mouse[0], 0, mouse[0], CONF.SCREEN_H, THEME.CROSSHAIR);
        self.renderer.draw_line(0, mouse[1], CONF.SCREEN_W, mouse[1], THEME.CROSSHAIR);
    }
    pub fn button(self: *Fui, x: i32, y: i32, w: i32, h: i32, label: [:0]const u8, color: u32, mouse: Mouse) bool {
        const hover: bool = self.check_hover(mouse, Rect.init(w, h, x, y));
        const text_cener = self.text_center(label, CONF.FONT_DEFAULT_SIZE);
        const text_x: i32 = x + @divFloor(w, 2) - text_cener[0];
        const text_y: i32 = y + @divFloor(h, 2) - text_cener[1];

        // self.renderer.draw_rect(x + CONF.SHADOW, y + CONF.SHADOW, w, h, THEME.SHADOW);
        self.renderer.draw_rect(x, y, w, h, color);
        self.renderer.draw_rect_lines(x, y, w, h, if (hover) THEME.MENU_FRAME_HOVER else THEME.MENU_FRAME);
        self.draw_text(label, text_x, text_y, CONF.FONT_DEFAULT_SIZE, if (hover) THEME.MENU_FRAME_HOVER else THEME.MENU_TEXT);

        return mouse.pressed and hover;
    }
    pub fn check_hover(self: *Fui, mouse: Mouse, target: Rect) bool {
        _ = self;
        return mouse.x >= target.x and mouse.x < target.x + target.w and
            mouse.y >= target.y and mouse.y < target.y + target.h;
    }
    pub fn draw_version(self: *Fui) void {
        const len = self.text_length(CONF.VERSION, CONF.FONT_DEFAULT_SIZE);
        const ver_x: i32 = self.pivotX(.bottom_right) - len;
        const ver_y: i32 = self.pivotY(.bottom_right);
        self.draw_text(CONF.VERSION, ver_x, ver_y, CONF.FONT_DEFAULT_SIZE, THEME.SECONDARY);
    }
    fn draw_base_popup(self: *Fui, message: [:0]const u8, bg_color: u32) Rect {
        const text_width: i32 = self.text_length(message, CONF.FONT_DEFAULT_SIZE);
        const popup_size = vec2(if (text_width < 256) 256 else text_width + 128, 128);
        const center = vec2(self.pivotX(.center), self.pivotY(.center));
        const popup_corner = vec2(center[0] - @divFloor(popup_size[0], 2), center[1] - @divFloor(popup_size[1], 2));

        const text_x: i32 = popup_corner[0] + @divFloor(popup_size[0] - text_width, 2);
        const text_y: i32 = popup_corner[1] + 24;

        const x: i32 = popup_corner[0];
        const y: i32 = popup_corner[1];
        const w: i32 = popup_size[0];
        const h: i32 = popup_size[1];

        self.renderer.draw_rect(x + 8, y + 8, w, h, THEME.SHADOW);
        self.renderer.draw_rect(x, y, w, h, bg_color);
        self.renderer.draw_rect_lines(x, y, w, h, THEME.LIGHT);
        self.draw_text(message, text_x, text_y, CONF.FONT_DEFAULT_SIZE, THEME.POPUP_MSG);
        return Rect.init(popup_size[0], popup_size[1], popup_corner[0], popup_corner[1]);
    }
    pub fn info_popup(self: *Fui, message: [:0]const u8, mouse: Mouse, bg_color: u32) ?bool {
        // Popup
        const popupv4: Rect = self.draw_base_popup(message, bg_color);
        const popup_corner = vec2(popupv4.x, popupv4.y);
        const popup_height = popupv4.h;

        // Button
        const button_height = 32;
        const button_width = 80;
        const button_x = self.pivotX(.center) - @divFloor(button_width, 2);
        const button_y = popup_corner[1] + popup_height - 50;
        const ok_clicked = self.button(button_x, button_y, button_width, button_height, "OK", THEME.OK, mouse);
        if (ok_clicked) return true;
        return null;
    }
    pub fn yes_no_popup(self: *Fui, message: [:0]const u8, mouse: Mouse) ?bool {
        // Popup
        const popupv4: Rect = self.draw_base_popup(message, THEME.POPUP);
        const popup_corner = vec2(popupv4.x, popupv4.y);
        const popup_size = vec2(popupv4.w, popupv4.h);

        // buttons
        const button_y = popup_corner[1] + popup_size[1] - 50;
        const button_height = 32;
        const button_width = 80;
        const no_x = popup_corner[0] + 24;
        const yes_x = popup_corner[0] + popup_size[0] - 80 - 24;

        const yes_clicked = self.button(yes_x, button_y, button_width, button_height, "Yes", THEME.YES, mouse);
        if (yes_clicked) return true;

        const no_clicked = self.button(no_x, button_y, button_width, button_height, "No", THEME.NO, mouse);
        if (no_clicked) return false;

        return null;
    }
};
